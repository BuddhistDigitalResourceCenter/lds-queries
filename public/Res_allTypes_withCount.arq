#QueryScope=General
#QueryReturnType=Table
#QueryResults=A table containing the Id and matching literal for the given query and language tag with the given limit
#QueryParams=L_NAME,LG_NAME,I_LIM
#QueryUrl=/Res_allTypes_withCount?L_NAME="'od zer"&LG_NAME=bo-x-ewts&I_LIM=1000

#param.L_NAME.type=string
#param.L_NAME.langTag=LG_NAME
#param.L_NAME.isLucene=true
#param.L_NAME.example=("'od zer" OR "ye shes")
#param.I_LIM.type=int
#param.I_LIM.desc=the maximum number of results

#output.?f.type=URI
#output.?f.desc=possible type
#output.?cid.type=int
#output.?cid.desc=number of occurrence of type

select ?f (count(distinct ?s) as ?cid)
WHERE {
    {
      { ?s :personName ?t }
      union
      { ?s :workTitle ?t }
      .
      { (?t ?sc ?lit) text:query ( rdfs:label ?L_NAME ) }
    }
    union
    {  (?s ?sc ?lit) text:query ( :eTextTitle ?L_NAME ) }
    union
    {
      { ?s a :Place }
      union
      { ?s a :Role }
      union
      { ?s a :Topic }
      union
      { ?s a :Corporation }
      union
      { ?s a :Lineage }
      .
      { (?s ?sc ?lit) text:query ( skos:prefLabel ?L_NAME ) }
      union
      { (?s ?sc ?lit) text:query ( skos:altLabel ?L_NAME ) }
      #union
      #{ (?s ?sc ?lit) text:query ( rdfs:comment ?L_NAME ) }
    }
    .
    { ?s a ?f . ?f rdfs:subClassOf bdo:Entity }
}
group by ?f
limit ?I_LIM
