#QueryScope=Etexts
#QueryReturnType=Graph
#QueryResults=Basic Info about an etext
#QueryParams=R_RES
#QueryUrl=/Etext_base?R_RES=bdr:UT4CZ5369_I1KG9127_0000

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Etext
#param.R_RES.desc=the etext on which info is needed

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct { 
   ?s ?p ?o .
   ?R_RES tmp:imageVolumeId ?imagevol .
   ?imagevol tmp:workLabel ?workpl .
   ?R_RES tmp:hasEtextInstance ?hasRepro .
  }
where {
  { 
    ?R_RES ?p ?o .
    BIND (?R_RES as ?s)
  } union {
    ?R_RES bdo:eTextInItem  ?s .
    ?s ?p ?o .
  } union {
    ?R_RES bdo:eTextInItem/bdo:itemForWork  ?s .
    ?s skos:prefLabel ?o .
    BIND (skos:prefLabel as ?p)
  } union {
    # get image volume ID (for iiif)
    ?R_RES :eTextIsVolume ?volNum .
    ?R_RES :eTextInInstance ?inInst .
    ?inInst :instanceReproductionOf ?isRepro ; :instanceOf ?work .
    ?isRepro :instanceHasVolume ?imagevol .
    ?imagevol a :ImageGroup .
    ?imagevol :volumeNumber ?volNum .
    ?work skos:prefLabel ?workpl .
  } union {
    #useful info to display etext in mirador
    ?R_RES :volumeOf/:instanceHasReproduction ?hasRepro .
    ?hasRepro a bdo:EtextInstance .
  }
}