#QueryScope=Volume
#QueryReturnType=Graph
#QueryResults=Outline of a work for the IIIF presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_volumeOutline?R_RES=bdr:W25094

#param.R_RES.type=URI
#param.R_RES.subtype=:Work
#param.R_RES.desc=the URI of the work

construct { 
  ?wp :workLocation ?loc .
  ?wp :workHasPart ?wpp .
  ?loc ?pl ?ol .
  ?wp skos:prefLabel ?wpl .
  ?wp :workPartIndex ?wpi .
  ?R_RES tmp:firstVolume ?vol .
}
where {
  {
    ?R_RES :workLinkTo?/:workHasPart* ?wp .
    ?wp skos:prefLabel ?wpl .
    optional {
      ?wp :workPartIndex ?wpi .
    }
    optional {
      ?wp :workLocation ?loc .
      ?loc ?pl ?ol .
    }
    optional {
      ?wp :workHasPart ?wpp .
    }
  } union {
    ?R_RES :workLinkTo?/bdo:workPartOf* ?rw .
    ?rw :workHasItem ?item .
    ?item a bdo:ItemImageAsset .
    ?R_RES :workLinkTo?/:workLocation ?loc .
    ?loc :workLocationVolume ?bvol .
    ?item :itemHasVolume ?vol .
    ?vol :volumeNumber ?bvol .
  } union {
    ?R_RES :workLinkTo?/:workHasItemImageAsset ?item .
    ?item :itemHasVolume ?vol .
    ?vol :volumeNumber 1 .
  }

}
