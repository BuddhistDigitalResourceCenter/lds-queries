#QueryScope=Volume
#QueryReturnType=Table
#QueryResults=Basic info about a volume for the IIIF Presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_volumeInfo?R_RES=bdr:V22084_I0886

#output.?workId.type=URI
#output.?workId.desc=the work/collection expressed by this volume
#output.?itemId.type=URI
#output.?itemId.desc=the item this volume belongs to
#output.?access.type=URI
#output.?access.desc=the access policy of the work/collection
#output.?license.type=URI
#output.?license.desc=the license of the work/collection
#output.?imageGroup.type=string
#output.?imageGroup.desc=the image group (internal BDRC data)
#output.?volumeNumber.type=int
#output.?volumeNumber.desc=the volume number
#output.?iiifManifest.type=URI
#output.?iiifManifest.desc=the (external) manifest URI

#param.R_RES.type=URI
#param.R_RES.subtype=:Volume
#param.R_RES.desc=the URI of the ItemImageAsset being investigated

select distinct ?workId ?itemId ?access ?license ?imageGroup ?volumeNumber ?volumeLabel ?iiifManifest where {
  ?R_RES a :VolumeImageAsset .
  optional { ?R_RES adm:legacyImageGroupRID ?imageGroup . }
  optional { ?R_RES :volumeNumber ?volumeNumber . }
  optional { ?R_RES skos:prefLabel ?volumeLabel . }
  optional { ?R_RES :hasIIIFManifest ?iiifManifest . }
  ?itemId :itemForWork ?workId ;
      :itemHasVolume ?R_RES .
  ?workId adm:access ?access ;
    adm:license ?license .
}
