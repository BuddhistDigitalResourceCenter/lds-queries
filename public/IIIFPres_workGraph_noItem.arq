#QueryScope=Work
#QueryReturnType=Graph
#QueryResults=Info about a work for the IIIF Presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_workGraph?R_RES=bdr:W22084

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Work
#param.R_RES.desc=the work on which info is needed

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct { ?s ?p ?o }
where {
  { 
    ?R_RES ?p ?o .
    BIND (?R_RES as ?s)
    FILTER (!isBlank(?o) || ?p = :workLocation)
  } union {
    ?R_RES :workCreator/skos:prefLabel ?o .
    BIND (?R_RES as ?s)
    BIND (tmp:workCreatorLit as ?p)
  } union {
    ?R_RES :workLocation ?s .
    ?s ?p ?o .
  } union {
    ?R_RES :workLocation/:workLocationWork/:workHasItemImageAsset ?o .
    BIND (?R_RES as ?s)
    BIND (tmp:inItem as ?p)
  } union {
    ?R_RES :workHasPart ?s .
    ?s ?p ?o .
    FILTER (!isBlank(?o))
  }
}
