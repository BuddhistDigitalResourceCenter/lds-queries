#QueryScope=Work
#QueryReturnType=Graph
#QueryResults=Info about a work for the IIIF Presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_workGraph_noItem?R_RES=bdr:W22084

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Work
#param.R_RES.desc=the work on which info is needed

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct { 
  ?s ?p ?o .
  ?o a ?ot .
  ?o skos:prefLabel ?ol .
  ?lt a ?ltt .
  ?R_RES tmp:rootAccess ?access .
  ?R_RES tmp:rootRestrictedInChina ?ric .
  ?R_RES tmp:rootLicense ?license .
  ?R_RES tmp:rootStatus ?status .
  ?R_RES tmp:inItem ?item .
  ?R_RES tmp:firstVolume ?vol .
}
where {
  { 
    ?R_RES ?p ?o .
    BIND (?R_RES as ?s)
  } union { 
    ?R_RES :workLinkTo ?s .
    ?s ?p ?o .
  } union { 
    ?R_RES :workLocation/:workLocationWork ?s .
    ?s ?p ?o .
  } union {
    ?R_RES :workLinkTo?/:workCreator/skos:prefLabel ?o .
    BIND (?R_RES as ?s)
    BIND (tmp:workCreatorLit as ?p)
  } union {
    ?R_RES :workLinkTo?/:workLocation ?s .
    ?s ?p ?o .
  } union {
    ?R_RES :workLinkTo?/bdo:workPartOf*/:workHasItem ?item .
    ?item a :ItemImageAsset .
    ?admItem adm:adminAbout ?item .
    ?admItem adm:access ?access .
    ?admItem adm:status ?status .
    ?admItem adm:restrictedInChina ?ric .
    ?admItem adm:contentLegal/adm:license ?license .
  } union {
    ?R_RES :workLocation/:workLocationWork/:workHasItem ?item .
    ?item a :ItemImageAsset .
    ?admItem adm:adminAbout ?item .
    ?admItem adm:access ?access .
    ?admItem adm:status ?status .
    ?admItem adm:restrictedInChina ?ric .
    ?admItem adm:contentLegal/adm:license ?license .
  } union {
    ?R_RES :workLinkTo?/bdo:workPartOf* ?rw .
    ?rw :workHasItem ?item .
    ?item a :ItemImageAsset .
    ?R_RES :workLinkTo?/:workLocation ?loc .
    ?loc :workLocationVolume ?bvol .
    ?item :itemHasVolume ?vol .
    ?vol :volumeNumber ?bvol .
  } union {
    ?R_RES :workLinkTo?/:workHasItemImageAsset ?item .
    ?item :itemHasVolume ?vol .
    ?vol :volumeNumber 1 .
  } union {
    ?R_RES :workLinkTo?/:workHasPart ?s .
    ?s ?p ?o .
  } union {
    ?R_RES :workHasPart ?s .
    ?s :workLinkTo ?o .
    ?o a ?ot .
    ?o skos:prefLabel ?ol .
  }
}
