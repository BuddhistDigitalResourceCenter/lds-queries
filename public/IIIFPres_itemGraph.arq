#QueryScope=Item
#QueryReturnType=Graph
#QueryResults=Info about an item for the IIIF Presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_itemGraph?R_RES=bdr:I22084

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Item
#param.R_RES.desc=the item on which info is needed

construct { ?s ?p ?o }
where {
  { 
    ?R_RES ?p ?o .
    BIND (?R_RES as ?s)
    FILTER (!isBlank(?o))
  } union {
    ?R_RES :itemForWork ?s .
    ?s ?p ?o .
    FILTER (sameTerm(?p, adm:access) || sameTerm(?p, adm:license) || sameTerm(?p, skos:prefLabel))
  } union {
    ?R_RES :itemHasVolume ?s .
    ?s ?p ?o .
    FILTER (sameTerm(?p, adm:access) || sameTerm(?p, adm:license) || sameTerm(?p, :volumeNumber) || sameTerm(?p, skos:prefLabel))
  }
}
