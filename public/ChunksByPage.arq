#QueryScope=Etext
#QueryReturnType=Graph
#QueryResults=Chunks in the asked page range
#QueryParams=R_RES,I_START,I_END
#QueryUrl=/query/graph/ChunksByPage?R_RES=bdr:UT4CZ5369_I1KG9127_0000&I_START=10&I_END=20

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Etext
#param.R_RES.desc=the etext we want the chunks of
#param.I_START.type=int
#param.I_START.desc=the index of the first page that's needed
#param.I_END.type=int
#param.I_END.desc=the index of the last page that's needed

construct {
  ?cc ?ccp ?cco .
  ?p ?pp ?po .
  ?l ?lp ?lo .
} 
where {
  {
    ?R_RES :eTextHasPage ?p .
    ?p :seqNum ?pnum
    FILTER (?pnum <= ?I_END && ?pnum >= ?I_START)
  }
  ?p ?pp ?po .
  ?R_RES :eTextHasPage ?pfirst .
  ?pfirst :seqNum ?I_START .
  ?pfirst :sliceStartChar ?pfirstStartChar .
  ?R_RES :eTextHasPage ?plast .
  ?plast :seqNum ?I_END .
  ?plast :sliceStartChar ?pendEndChar .
  ?R_RES :eTextHasChunk ?cc .
  ?cc  :sliceStartChar ?chunkstart ;
       :sliceEndChar ?chunkend .
  FILTER (?chunkstart <= ?pendEndChar && ?chunkend >= ?pfirstStartChar)
  ?cc ?ccp ?cco .
  ?p :pageHasLine ?l .
  ?l ?lp ?lo .
}
