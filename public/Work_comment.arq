#QueryScope=Work
#QueryReturnType=Table
#QueryResults=A table containing the Id, the name, the types of the commentaries of works whose name contains the NAME param value, along with the root text ID and preferred name
#QueryParams=L_NAME
#QueryUrl=?searchType=Work_comment&L_NAME=rgyud bla ma

#param.L_NAME.type=string
#param.L_NAME.isLucene=false
#param.L_NAME.example=rgyud bla ma

#output.?comment.type=URI
#output.?comment.desc=the resource URI of a commentary of a work whose name contains L_NAME
#output.?comment_types.type=string
#output.?comment_types.desc=the list of commentary type applicable to the commentary
#output.?comment_name.type=string
#output.?comment_name.desc=the name of the commentary
#output.?root.type=URI
#output.?root.desc=the resource URI of the root text, whose name contains L_NAME
#output.?root_name.type=string
#output.?root_name.desc=the name of the root text

select ?comment (GROUP_CONCAT(DISTINCT ?comment_type;  SEPARATOR=" <> ") AS ?comment_types)  ?comment_name ?root ?root_name
where {
	?comment :workIsAbout ?root;
             :workGenre ?g ;
  		     skos:prefLabel ?comment_name.
    ?g skos:prefLabel ?comment_type .
  	?root  skos:prefLabel ?root_name .
    FILTER ((contains(?comment_type, "commentary" ))
         || (contains(?comment_type, "rnam bshad" ))
         || (contains(?comment_type, "'grel pa" ))
         || (contains(f:SankritFilter(?comment_type), "ṭīkā" ))
         || (contains(f:SankritFilter(?comment_type), "vyākhyā" )))
    FILTER (contains(?root_name, ?L_NAME ))

}
group by ?comment ?comment_name ?root ?root_name
