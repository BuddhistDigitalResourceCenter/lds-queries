#QueryScope=Volume
#QueryReturnType=Graph
#QueryResults=Outline of a volume for IIIF presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_volumeOutline?R_RES=bdr:V22084_I0886

#param.R_RES.type=URI
#param.R_RES.subtype=:Volume
#param.R_RES.desc=the URI of the volume being investigated



construct { 
  ?R_RES ?p ?o .
  ?item :itemImageAssetForWork ?work .
  ?work adm:license ?wl .
  ?work adm:access ?wa .
  ?work :workHasPart ?wp .
  #?part ?pp ?op .
  ?ascPart ?pap ?oap .
  ?loc ?pl ?ol .
}
where {
  {
    {
      ?R_RES :volumeOf/:itemImageAssetForWork ?work ;
             :volumeNumber ?volnum .
      ?loc :workLocationWork ?work .
      ?loc :workLocationVolume ?bvol .
      # see https://lists.apache.org/thread.html/ec4c7fb703748e7d0d70d8e3baa1c2fbb04842b7494c9097df7dfd0f@%3Cusers.jena.apache.org%3E
      FILTER ((?bvol <= ?volnum && NOT EXISTS {?loc :workLocationEndVolume ?evol}) || (?bvol <= ?volnum  && EXISTS {?loc :workLocationEndVolume ?evol FILTER (?evol <= ?volnum)}))
    }
    ?work :workHasPart ?wp .
    ?loc ?pl ?ol .
    ?part :workLocation ?loc .
    #?part ?pp ?op .
    ?part :workPartOf* ?ascPart .
    optional {?ascPart :workPartOf ?wpo}
    filter (bound(?wpo))
    ?ascPart ?pap ?oap .
  } union {
    ?R_RES :volumeOf ?item .
    ?R_RES ?p ?o .
    ?item :itemImageAssetForWork ?work .
    ?work adm:license ?wl .
    ?work adm:access ?wa .
  }
}



