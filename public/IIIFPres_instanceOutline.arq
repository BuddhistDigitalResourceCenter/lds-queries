#QueryScope=Instance
#QueryReturnType=Graph
#QueryResults=Outline of an instance for the IIIF presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_instanceOutline?R_RES=bdr:MW25094

#param.R_RES.type=URI
#param.R_RES.subtype=:Instance
#param.R_RES.desc=the URI of the instance

construct { 
  ?wp :contentLocation ?loc .
  ?wp :hasPart ?wpp .
  ?loc ?pl ?ol .
  ?wp skos:prefLabel ?wpl .
  ?wp :partIndex ?wpi .
  ?R_RES tmp:firstImageGroup ?vol .
}
where {
  {
    ?R_RES :virtualLinkTo?/:hasPart* ?wp .
    ?wp skos:prefLabel ?wpl .
    optional {
      ?wp :workPartIndex ?wpi .
    }
    optional {
      ?wp :workLocation ?loc .
      ?loc ?pl ?ol .
    }
    optional {
      ?wp :workHasPart ?wpp .
    }
  } union {
    ?R_RES :virtualLinkTo?/bdo:workPartOf* ?rw .
    ?rw :instanceHasReproduction ?iinstance .
    ?iinstance a bdo:ImageInstance .
    ?R_RES :virtualLinkTo?/:workLocation ?loc .
    ?loc :workLocationVolume ?bvol .
    ?iinstance :instanceHasVolume ?vol .
    ?vol :volumeNumber ?bvol .
  } union {
    ?R_RES :virtualLinkTo?/:instanceHasReproduction ?iinstance .
    ?iinstance a bdo:ImageInstance .
    ?iinstance :instanceHasVolume ?vol .
    ?vol :volumeNumber 1 .
  }

}
