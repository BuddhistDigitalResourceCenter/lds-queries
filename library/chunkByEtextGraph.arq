#QueryScope=Etexts
#QueryReturnType=Graph
#QueryResults=All works having etext whose chunks contain the given L_NAME parameter value, along with Topic and WorkMainAuthor facets
#QueryParams=R_RES,L_NAME,LG_NAME
#QueryUrl=/lib/chunkByEtextGraph?R_RES=bdr:UT1PD90129_014_0029&L_NAME="de bzhin gshegs pa'i snying po"&LG_NAME=bo-x-ewts

#param.L_NAME.type=string
#param.L_NAME.langTag=LG_NAME
#param.L_NAME.isLucene=true
#param.L_NAME.example="ye shes"

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Etext
#param.R_RES.desc=the Etext on which info is needed


PREFIX  :     <http://purl.bdrc.io/ontology/core/>
PREFIX  bf:   <http://id.loc.gov/ontologies/bibframe/>
PREFIX  tbr:  <http://purl.bdrc.io/ontology/toberemoved/>
PREFIX  owl:  <http://www.w3.org/2002/07/owl#>
PREFIX  f:    <java:io.bdrc.ldspdi.sparql.functions.>
PREFIX  xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX  skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  adm:  <http://purl.bdrc.io/ontology/admin/>
PREFIX  vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX  bdo:  <http://purl.bdrc.io/ontology/core/>
PREFIX  bdr:  <http://purl.bdrc.io/resource/>
PREFIX  rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  dcterms: <http://purl.org/dc/terms/>
PREFIX  text: <http://jena.apache.org/text#>
PREFIX  foaf: <http://xmlns.com/foaf/0.1/>

construct{
  ?etext tmp:forWork ?s .
  ?etext tmp:hasChunk ?chunk.
  ?etext bdo:creatorMainAuthor ?author.
  ?etext tmp:authorName ?author_name .
  ?etext bdo:workIsAbout ?about .
  ?etext bdo:workGenre ?workgenre .
  ?etext tmp:workLabel ?workLabel .
  ?etext a :Etext .
  ?etext bdo:eTextTitle ?etextTitle .
  ?etext bdo:eTextVolumeIndex ?volIndex .
  ?etext bdo:eTextIsVolume ?isVolume .
  ?chunk a :EtextChunk .
  ?chunk :eTextHasChunk ?lit .
  ?chunk bdo:seqNum ?seqNum . 
  ?chunk bdo:sliceStartChunk ?startChunk .
  ?chunk bdo:sliceEndChunk ?endChunk
}
where
{
  BIND (?R_RES AS ?etext)
  (?chunk ?score ?lit) text:query ( :chunkContents ?L_NAME "highlight:") .
  ?etext :eTextHasChunk ?chunk .
  ?chunk bdo:seqNum ?seqNum .
  ?chunk bdo:sliceStartChunk ?startChunk .
  ?chunk bdo:sliceEndChunk ?endChunk .
  ?s :workHasItemEtext/bdo:itemHasVolume/bdo:volumeHasEtext/bdo:eTextResource  ?etext.
  ?etext bdo:eTextTitle ?etextTitle .
  optional{?etext bdo:eTextIsVolume ?isVolume .}
  optional{?etext bdo:eTextVolumeIndex ?volIndex .}
  optional{?s bdo:workIsAbout ?about .
    ?about a :Topic}.
  optional {?s bdo:workGenre ?workgenre .
  ?workgenre a :Topic} 
  optional{?s skos:prefLabel ?workLabel .}
  Optional{ ?s bdo:creatorMainAuthor ?author .
  ?author skos:prefLabel ?author_name }.
  
}
order by desc(?score)