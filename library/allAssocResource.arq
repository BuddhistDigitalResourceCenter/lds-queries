#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All resources preflabel related to the given Resource
#QueryParams=R_RES
#QueryUrl=/lib/allAssocResource?R_RES=bdr:P1583

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the resource we want labels of associates of

prefix oa:     <http://www.w3.org/ns/oa#>

construct {
  ?o skos:prefLabel ?l.
  ?rev ?pp  ?R_RES .
  ?rev skos:prefLabel ?revl .
  ?bno skos:prefLabel ?bnol .
  ?who ?pr ?whol .
  ?rcn ?ppr ?rcl .
  ?tp skos:prefLabel ?tpl .
  ?imA bdo:itemHasVolume ?iV .
  ?hEx bdo:workHasRoot ?hExW .
  ?hExW skos:prefLabel ?hExO .
  ?R_RES bdo:workHasRoot ?rw .
  ?rw skos:prefLabel ?rwO .
  ?wp bdo:workPartIndex ?wpPi .
  ?wp bdo:workPartType ?wpPt .
  ?o bdo:originalRecord ?ori .
  ?o bdo:contentProvider ?provL .
  ?rs ?rsp ?rso .
  ?ann ?annp ?anno .
  ?annb ?annbp ?annbo .
  ?annbo ?annbop ?annboo .
}

where {
  {
    ?R_RES ?p ?o .
    ?o skos:prefLabel ?l .
    optional {
      ?o bdo:contentProvider ?prov .
      ?prov skos:prefLabel ?provL .
      ?o bdo:originalRecord ?ori .
    }
  }
  union
  {
    # reifications related to the main resource (this does not currently fetch the reified statements related to subresources)
    ?rs rdf:subject ?R_RES ;
        ?rsp ?rso .
    ?ann oa:hasTarget ?rs ;
        ?annp ?anno .
    optional {
      ?ann oa:hasBody ?annb .
      ?annb ?annbp ?annbo .
      optional {
        ?annb :supportedByLocation ?annbloc .
        ?annbloc ?annblocp ?annbloco .
        ?annbloc :workLocationWork ?annblocWork .
        ?annblocWork skos:prefLabel ?annblocWorkLabel .
      }
      optional {
        ?annb :supportedByAssertion ?annba .
        ?annba ?annbap ?annbao .
        optional {
           ?annba :workLocation ?annbaloc .
           ?annbaloc ?annbalocp ?annbaloco .
           ?annbaloc :workLocationWork ?annbalocWork .
           ?annbalocWork skos:prefLabel ?annbalocWorkLabel .
        }
      }
    }
  }

  union
  {
    ?rev ?pp  ?R_RES .
    ?rev skos:prefLabel ?revl .
  }
  union
  {
    # blank nodes
    ?R_RES ?tmpP ?bn .
    FILTER (isBlank(?bn))
    ?bn ?bnp ?bno .
    ?bno skos:prefLabel ?bnol .
  }
  #for Lineage models
  union
  {
    ?R_RES bdo:lineageHolder ?hold .
    ?hold ?pr ?who .
    ?hold rdf:type ?type .
    ?who rdf:type ?tt .
    ?who skos:prefLabel ?whol .
    ?hold bdo:lineageReceived ?rc .
    ?rc ?ppr ?rcn .
    ?rcn skos:prefLabel ?rcl .
  }
  #for Topic models
  union
  {
    ?R_RES rdfs:seeAlso ?tp .
    ?tp skos:prefLabel ?tpl
  }
  #for Work
  union
  {
    ?R_RES bdo:workHasPart ?wp .
    ?wp bdo:workPartIndex ?wpPi .
    optional {?wp bdo:workPartType ?wpPt . } .
  }
  union
  {
    # root work is a parent with no parent
    ?R_RES bdo:workPartOf+ ?rw .
    optional { ?rw bdo:workPartOf ?rwP }
    FILTER (!bound(?rwP))
    ?rw skos:prefLabel ?rwO .
  }
  union
  {
    ?R_RES bdo:workHasItemImageAsset ?imA .
    ?imA bdo:itemHasVolume ?iV .
  }
  union
  {
    ?R_RES bdo:workHasExpression ?hEx .
    # same as above, root work is a parent with no parent
    ?hEx bdo:workPartOf+ ?hExW .
    optional { ?hExW bdo:workPartOf ?hExWP }
    FILTER (!bound(?hExWP))
    ?hExW skos:prefLabel ?hExO .
  }
}
