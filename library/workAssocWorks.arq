#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All works related to the given Work, along with their LICENSE + ACCESS + LANGSCRIPT + STATUS facets.
#QueryParams=R_RES
#QueryUrl=/lib/workAssocWorks?R_RES=W22003

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the referenced Work

construct{
  ?w a :Work .
  ?w skos:prefLabel ?l.
  ?w adm:license ?lic .
  ?w adm:access ?acc .
  ?w adm:status ?st .  
  ?w bdo:workLangScript ?ls.
  ?w bdo:workGenre ?g .
  ?w1 a :Work .
  ?w1 skos:prefLabel ?l1.
  ?w1 adm:license ?lic1 .
  ?w1 adm:access ?acc1 .
  ?w1 adm:status ?st1 .
  ?w1 bdo:workLangScript ?ls1.
  ?w1 bdo:workGenre ?g1 .
  ?w1 ?p1 ?R_RES .
}
where {
  {
	?R_RES ?p ?w .
    ?w skos:prefLabel ?l.
  	optional{?w bdo:workPartOf* ?po .
        ?po adm:license ?lic .
        ?po adm:access ?acc .
        ?po adm:status ?st .
        ?po bdo:workLangScript ?ls. 
    }
    optional{
    	?w bdo:workGenre ?g .
  		?g a :Topic
  	}
    ?w a :Work.
  }
  union{
    ?w1 ?p1 ?R_RES .
    ?w1 skos:prefLabel ?l1.
    optional{?w1 bdo:workPartOf* ?po1 .
    ?po1 adm:license ?lic1 .
    ?po1 adm:access ?acc1 .
    ?po1 adm:status ?st1 .
    ?po1 bdo:workLangScript ?ls1. 
    }
    optional{
    	?w1 bdo:workGenre ?g1 .
  		?g1 a :Topic
  	}
  	?w1 a :Work .
  }
}
