#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All works having etext whose chunks contain the given L_NAME parameter value, along with Topic and WorkMainAuthor facets
#QueryParams=L_NAME,LG_NAME,I_LIM
#QueryUrl=/lib/chunksFacetGraph?L_NAME="de bzhin gshegs"&LG_NAME=bo-x-ewts&I_LIM=1000

#param.L_NAME.type=string
#param.L_NAME.langTag=LG_NAME
#param.L_NAME.isLucene=true
#param.L_NAME.example="ye shes"

construct{
  
  ?chunk a :EtextChunk .
  ?chunk :eTextHasChunk ?lit .
  ?chunk bdo:seqNum ?seqNum . 
  ?chunk bdo:sliceStartChunk ?startChunk .
  ?chunk bdo:sliceEndChunk ?endChunk .
  ?chunk tmp:forEtext ?etext .
  ?chunk tmp:forWork ?s .
  ?chunk tmp:workLabel ?workLabel .
  ?chunk bdo:creatorMainAuthor ?author.
  ?chunk tmp:authorName ?author_name .
  ?chunk tmp:etextAbout ?about .
  ?chunk tmp:etextGenre ?genre .
  ?chunk bdo:eTextTitle ?etextTitle .
  ?chunk bdo:eTextVolumeIndex ?volIndex .
  ?chunk bdo:eTextIsVolume ?isVolume .
}
where
{
  (?chunk ?score ?lit) text:query ( :chunkContents ?L_NAME ?I_LIM "highlight:") .
  ?etext :eTextHasChunk ?chunk .
  ?chunk bdo:seqNum ?seqNum .
  ?chunk bdo:sliceStartChunk ?startChunk .
  ?chunk bdo:sliceEndChunk ?endChunk .
  ?s :workHasItemEtext/bdo:itemHasVolume/bdo:volumeHasEtext/bdo:eTextResource  ?etext.
  ?etext bdo:eTextTitle ?etextTitle .
  optional{?etext bdo:eTextIsVolume ?isVolume .}
  optional{?etext bdo:eTextVolumeIndex ?volIndex .}
  optional{?s bdo:workIsAbout ?about .
    ?about a :Topic}.
  optional {?s bdo:workGenre ?workgenre .
  ?workgenre a :Topic} 
  optional{?s skos:prefLabel ?workLabel .}
  Optional{ ?s bdo:creatorMainAuthor ?author .
  ?author skos:prefLabel ?author_name }.
  
}
order by desc(?score)
