#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All works related to the given Work, along with their LICENSE + ACCESS + LANGSCRIPT + STATUS facets.
#QueryParams=R_RES
#QueryUrl=/lib/workSearchGraph?L_NAME="'od zer"&LG_NAME=bo-x-ewts

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the referenced Work

construct {
  ?w skos:prefLabel ?l.
  ?w adm:license ?lic .  
  ?w adm:status ?st .
  ?w adm:access ?acc .
  ?w bdo:workLangScript ?ls .
  ?w bdo:workIsAbout ?topic .
  ?w bdo:workGenre ?g.
  ?w <http://purl.bdrc.io/ontology/core/rootTax> ?tx.
}

where {
  {
  	?w bdo:workIsAbout ?R_RES .
  	?w bdo:workIsAbout ?topic .
  	?w skos:prefLabel ?l.
  	?w adm:license ?lic.
  	?w adm:status ?st .
  	?w adm:access ?acc .
  	?w bdo:workLangScript ?ls.
  }
  union
  { 
    ?w bdo:workIsAbout ?R_RES .
  	?w bdo:workIsAbout ?topic .    
  	?w skos:prefLabel ?l.
    ?w bdo:workPartOf+ ?po .
    ?po adm:license ?lic .
    ?po adm:access ?acc .
    ?po adm:status ?st .
    ?po bdo:workLangScript ?ls. 
  }
  optional{?w bdo:workGenre ?g .
  		   ?g :taxSubclassOf+ ?tx .
  		   FILTER EXISTS {?tx :taxSubclassOf bdr:O9TAXTBRC201605}
    }
}