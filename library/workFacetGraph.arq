#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All works whose various literals contain the given L_NAME parameter value, along with their LICENSE + ACCESS + LANGSCRIPT + STATUS facets.
#QueryParams=L_NAME,LG_NAME
#QueryUrl=/lib/workFacetGraph?L_NAME="'od zer"&LG_NAME=bo-x-ewts

#param.L_NAME.type=string
#param.L_NAME.langTag=LG_NAME
#param.L_NAME.isLucene=true
#param.L_NAME.example="ye shes"

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

CONSTRUCT
  {
    ?res adm:license ?license .
    ?res adm:status ?st .
    ?res a ?type .
    ?res adm:access ?access .
    ?res bdo:workIsAbout ?t .
    ?res bdo:workGenre ?g .
    ?res bdo:workLangScript ?ls .
    ?res skos:prefLabel ?l .
    ?res tmp:titleMatch ?lit .
    ?res tmp:matchScore ?sc .
    ?res tmp:labelMatch ?labelMatch.
    ?res bdo:workPartType ?wPT .
    ?res bdo:workHasPart ?wHP .
    ?res bdo:workExpressionOf ?wE .
    ?res tmp:prefLabelExpressionOf ?litE .
    ?res bdo:workHasExpression ?wH .
    ?res tmp:prefLabelHasExpression ?litH .
  }
WHERE 
  {
    (?title ?sc ?lit) text:query ( rdfs:label ?L_NAME "highlight:" ) .
    ?res :workTitle ?title .
    ?res a ?type .
    ?res skos:prefLabel ?l .
    optional {
      ?res skos:prefLabel ?labelMatch.
      Filter ( str(?labelMatch) = str(?lit) )
    }
    ?res bdo:workPartOf* ?rw .
    ?rw :isRoot true .
    ?resAdm adm:adminAbout ?rw ;
            adm:status ?st .
    optional {
        ?rw  bdo:workLangScript ?ls . 
    } .
    optional { ?res bdo:workHasPart ?wHP . } .
    optional { ?res bdo:workPartType ?wPT . } .
    optional {
      ?res bdo:workHasExpression ?wH .
      ?wH skos:prefLabel ?litH .
    } .
    optional { ?res bdo:workGenre ?g } .
    optional {
      ?res bdo:workIsAbout ?t .
      ?t a :Topic
    } .
    optional {
      ?rw :workHasItem ?item .
      ?item a :ItemImageAsset .
      ?itemAdm adm:adminAbout ?item .
      ?itemAdm adm:access ?access .
      ?itemAdm adm:contentLegal/adm:license ?license .
    } .
  }
