#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All works whose various literals contain the given L_NAME parameter value, along with their LICENSE + ACCESS + LANGSCRIPT + STATUS facets.
#QueryParams=L_NAME,LG_NAME
#QueryUrl=/lib/workFacetGraph?L_NAME="'od zer"&LG_NAME=bo-x-ewts

#param.L_NAME.type=string
#param.L_NAME.langTag=LG_NAME
#param.L_NAME.isLucene=true
#param.L_NAME.example="ye shes"

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

CONSTRUCT
{
    ?res adm:status ?st .
    ?res tmp:matchScore ?sc .
    ?res tmp:labelMatch ?labelMatch.
  
    ?res ?resp ?reso .
    ?res ?respp ?resoo .
    ?res ?instancep ?instance .

    ?instance skos:prefLabel ?instancel .
    ?instance a ?instancetype .
    ?instance tmp:instanceAccess ?instanceAccess .
 
    ?res tmp:author ?author .
    ?agent skos:prefLabel ?agentL .
  
    ?resoo skos:prefLabel ?ol .

}
WHERE 
{
  {
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
    ?res a/rdfs:subClassOf* bdo:Work .
    ?res ?resp ?reso .
    FILTER (?resp = bdo:langage || ?resp = skos:altLabel || ?resp = skos:prefLabel || ?resp = rdf:type)
    
  } union {
    
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
    
    ?res bdo:creator ?creator .
    ?creator ?creatorp ?creatoro .
    ?creator bdo:role ?role .
    ?creator bdo:agent ?agent .
    ?agent skos:prefLabel ?agentL .
    
    bind( if(?role = bdr:R0ER0014 || ?role = bdr:R0ER0016 || ?role = bdr:R0ER0019 || ?role = bdr:R0ER0025, ?agent, false) as ?author)

  } union {
    
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
    ?res a/rdfs:subClassOf* bdo:Work .
    
    ?res ?respp ?resoo .
    FILTER(?respp = bdo:workTranslationOf || ?respp = bdo:serialMemberOf  || ?respp = bdo:workIsAbout || ?respp = bdo:workGenre)
    ?resoo skos:prefLabel ?ol .

  } union {
    
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .

    ?res ?instancep ?instance .
    FILTER(?instancep = bdo:workHasInstance || ?instancep = bdo:serialHasInstance)
    ?instance skos:prefLabel ?instancel .
    ?instance a ?instancetype .
    ?instanceAdm adm:adminAbout ?instance .
    #?instanceAdm adm:access ?instanceAccess .
  }
}
