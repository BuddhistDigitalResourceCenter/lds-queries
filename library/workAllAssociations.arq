#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All resources associated with the given Work.
#QueryParams=R_RES
#QueryUrl=/lib/workAllAssociations?R_RES=W19740

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the referenced work

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct {
  #associated Works
  ?work a ?type .
  ?work skos:prefLabel ?worklabel .
  ?work bdo:workIsAbout ?workAbout .
  ?work bdo:workLangScript ?workscript.
  ?work bdo:workType ?type .
  ?work bdo:workGenre ?workgenre .

  ?work adm:license ?rootWorkLicence.
  ?work adm:status ?rootWorkStatus .
  ?work adm:access ?rootWorkAccess .
  ?work bdo:workLangScript ?rootWorkScript.
  ?work bdo:workHasRoot ?rootWork .
  ?work tmp:relationType ?relationType .
  ?work tmp:rootPrefLabel ?rootWorkPrefLabel .

  #associated Lineages
  ?lineage a :Lineage .
  ?lineage skos:prefLabel ?lineagePrefLabel . 
  ?lineage tmp:relationType bdo:lineageObject .
}
where {
    #associated Works
  {
      ?work :workIsAbout ?R_RES .
      BIND(:workIsAbout as ?relationType)
      # we don't want expressions, just the most abstract
      OPTIONAL{
        ?work bdo:workExpressionOf ?workExpressed .
      }
      FILTER (!bound(?workExpressed))
      ?work skos:prefLabel ?worklabel .
      ?work a ?type .
      optional {
        ?work bdo:workIsAbout ?workAbout .
        ?workAbout a bdo:Topic .
      }
      ?work bdo:workPartOf* ?rw .
      ?rw :isRoot true .
      ?rw skos:prefLabel ?rootWorkPrefLabel .
      ?resAdm adm:adminAbout ?rw ;
              adm:status ?rootWorkStatus .

      optional {?work bdo:workLangScript ?workscript .}

      optional {?work bdo:workGenre ?workgenre .}      
      optional {
        ?rw :workHasItem ?item .
        ?item a :ItemImageAsset .
        ?itemAdm adm:adminAbout ?item .
        ?itemAdm adm:access ?rootWorkAccess .
        ?itemAdm adm:contentLegal/adm:license ?rootWorkLicence .
      }
      optional { ?rw  bdo:workLangScript ?rootWorkScript . } .  }
  #associated Lineages
  union
  {
    ?lineage bdo:lineageObject ?R_RES .
    ?lineage skos:prefLabel ?lineagePrefLabel
  }
}
