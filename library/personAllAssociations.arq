#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All resources associated with the given Person.
#QueryParams=R_RES
#QueryUrl=/lib/personAllAssociations?R_RES=P360

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the referenced Person

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct
{
  #Associated people
  ?o tmp:relationType ?peoplerel .
  ?o a :Person .
  ?o bdo:personGender ?g .
  ?o skos:prefLabel ?l .

  #Associated works
  #?w ?wrel ?R_RES .
  ?w tmp:relationType ?wrel .
  ?w a ?type .
  ?w skos:prefLabel ?ll.
  ?w adm:license ?lic.
  ?w adm:status ?st .
  ?w adm:access ?acc .
  ?w bdo:workLangScript ?ls.
  ?w bdo:workGenre ?wG .
  ?w bdo:workIsAbout ?wAB .
  ?w bdo:workPartType ?wPT.
  ?w bdo:workObjectType ?wOT.
  ?w bdo:workHasPart ?wHP.
  ?w bdo:workPartOf ?wPO.
  ?w bdo:workExpressionOf ?wE.
  ?w tmp:prefLabelExpressionOf ?litE.
  ?w bdo:workHasExpression ?wH.
  ?w tmp:prefLabelHasExpression ?litH.

  ?w bdo:workLangScript ?rwls.
  ?w bdo:workHasRoot ?rw .
  ?w tmp:rootPrefLabel ?rwO .

  #Associated places
  ?place a :Place .
  ?place tmp:relationType ?plrel .
  ?place skos:prefLabel ?plabel .

  #Associated Lineages
  ?lin a :Lineage .
  ?lin skos:prefLabel ?linl .
  ?lin ?pr ?R_RES .
}

where {
  #Associated people
  {
    ?R_RES ?peoplerel ?o .
    ?o a :Person .
    ?o skos:prefLabel ?l .
    OPTIONAL{ ?o bdo:personGender ?g } .
  }
  #Associated works
  union
  {
    {
      ?w :creator ?agent .
      ?agent :agent ?R_RES ; :role ?creaRole .
      ?creaRole skos:prefLabel ?roleLabel . 
      ?R_RES skos:prefLabel ?creaLabel . 
      BIND(?roleLabel as ?wrel)
    }
    union 
    {
      ?w :workIsAbout ?R_RES .
      BIND(:workIsAbout as ?wrel)
    }
    ?w a ?type .
    ?w skos:prefLabel ?ll .

    optional {
      ?w bdo:workPartOf* ?rw .
      ?rw :isRoot true ;
          adm:status ?rwst ;
          skos:prefLabel ?rwO .

      ?resAdm adm:adminAbout ?rw ;
              adm:status ?st .

      optional {?rw bdo:workLangScript ?rwls .}

      optional {
        ?rw :workHasItem ?item .
        ?item a :ItemImageAsset .
        ?itemAdm adm:adminAbout ?item .
        ?itemAdm adm:access ?acc .
        ?itemAdm adm:contentLegal/adm:license ?lic .
      }
    }

    optional {?w bdo:workLangScript ?ls .}
    optional {?w bdo:workObjectType ?wOT .}
     optional {?w bdo:workPartType ?wPT .}
    optional {?w bdo:workGenre ?wG .}
    optional {
       ?w bdo:workIsAbout ?wAB .
       ?wAB a :Topic .
    }
    
    OPTIONAL{?w bdo:workHasPart ?wHP . } .
    OPTIONAL{?w bdo:workPartType ?wPT . } .
    OPTIONAL{
        ?w bdo:workExpressionOf ?wE.
        ?wE skos:prefLabel ?litE.
       }.
    OPTIONAL{
        ?w bdo:workHasExpression ?wH.
        ?wH skos:prefLabel ?litH.
    }
  }
  #Associated places
  # in the person events
  union
  {
    ?R_RES bdo:personEvent ?e.
    ?e a ?plrel .
    ?e :eventWhere ?place .
    ?place skos:prefLabel ?plabel .
  }
  # in other events
  union
  {
    ?e :eventWho ?R_RES .
    ?a a ?plrel .
    ?e :eventWhere ?place .
    ?place skos:prefLabel ?plabel .
  }
  #Associated Lineages
  union
  {
    ?lin bdo:lineageHolder ?hold .
    ?hold ?pr ?R_RES .
    ?lin skos:prefLabel ?linl
  }
}
