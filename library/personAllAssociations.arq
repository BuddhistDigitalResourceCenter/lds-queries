#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All resources associated with the given Person.
#QueryParams=R_RES
#QueryUrl=/lib/personAllAssociations?R_RES=P360

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the referenced Person

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct
{
  #Associated people
  ?o tmp:relationType ?peoplerel .
  ?o a :Person .
  ?o bdo:personGender ?g .
  ?o skos:prefLabel ?l .

  #Associated works
  #?w ?wrel ?R_RES .
  ?w tmp:relationType ?wrel .
  ?w a :Work .
  ?w skos:prefLabel ?ll.
  ?w adm:license ?lic.
  ?w adm:status ?st .
  ?w adm:access ?acc .
  ?w bdo:workLangScript ?ls.

  ?w adm:license ?rwlic.
  ?w adm:status ?rwst .
  ?w adm:access ?rwacc .
  ?w bdo:workLangScript ?rwls.
  ?w bdo:workHasRoot ?rw .
  ?w tmp:rootPrefLabel ?rwO .

  #Associated places
  ?place a :Place .
  ?place tmp:relationType ?plrel .
  ?place skos:prefLabel ?plabel .

  #Associated Lineages
  ?lin a :Lineage .
  ?lin skos:prefLabel ?linl .
  ?lin ?pr ?R_RES .
}

where {
  #Associated people
  {
    ?R_RES ?peoplerel ?o .
    ?o a :Person .
    ?o skos:prefLabel ?l .
    OPTIONAL{ ?o bdo:personGender ?g } .
  }
  #Associated works
  union
  {
    ?w ?wrel ?R_RES .
    ?w a :Work .
    ?w skos:prefLabel ?ll .
    optional {?w adm:license ?lic .}
    optional {?w adm:status ?st .}
    optional {?w adm:access ?acc .}
    optional {?w bdo:workLangScript ?ls .}
    optional {?w bdo:workType ?wT .}
    optional {?w bdo:workGenre ?wG .}
    optional {
      ?w bdo:workPartOf+ ?rw .
      optional { ?rw bdo:workPartOf ?rwP }
      FILTER (!bound(?rwP))
      ?rw skos:prefLabel ?rwO .
      optional {?rw adm:license ?rwlic .}
      optional {?rw adm:status ?rwst .}
      optional {?rw adm:access ?rwacc .}
      optional {?rw bdo:workLangScript ?rwls .}
    }
  }
  #Associated places
  # in the person events
  union
  {
    ?R_RES bdo:personEvent ?e.
    ?e a ?plrel .
    ?e :eventWhere ?place .
    ?place skos:prefLabel ?plabel .
  }
  # in other events
  union 
  {
    ?e :eventWho ?R_RES .
    ?a a ?plrel .
    ?e :eventWhere ?place .
    ?place skos:prefLabel ?plabel .
  }
  #Associated Lineages
  union
  {
    ?lin bdo:lineageHolder ?hold .
    ?hold ?pr ?R_RES .
    ?lin skos:prefLabel ?linl
  }
}
