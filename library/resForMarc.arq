#QueryScope=Work
#QueryReturnType=Graph
#QueryResults=A graph for the given work
#QueryParams=R_RES
#QueryUrl=/lib/resForMarc?R_RES=bdr:W23819

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct {
  ?R_RES ?p ?o .
  ?o ?op ?oo .
  ?t skos:prefLabel ?to .
  ?series skos:prefLabel ?seriesl .
  ?part ?partp ?parto .
  ?pname ?pnamep ?pnameo .
  ?pevent ?peventp ?pevento .
  ?R_RES tmp:publishedYear ?publishedYear .
  ?o bdo:personEvent ?pevent .
  ?pevent ?peventp ?pevento .
  ?ols ?olsp ?olso .
  ?lango ?langop ?langoo .
  ?scripto ?scriptop ?scriptoo .
  ?ools ?oolsp ?oolso .
  ?olango ?olangop ?olangoo .
} where {
  {
    ?R_RES ?p ?o .
  }
  union {
    ?R_RES bdo:workIsAbout ?t .
    ?t skos:prefLabel ?to .
  }
  union {
    ?R_RES bdo:workHasPart+ ?part .
    ?part ?partp ?parto .
    FILTER (?partp = skos:prefLabel || ?partp = bdo:workHasPart || ?partp = bdo:workPartTreeIndex)
  }
  union {
    ?R_RES bdo:workNumberOf ?series .
    ?series skos:prefLabel ?seriesl .
  }
  union {
    ?p rdfs:subclassOf* :workCreator .
    ?R_RES ?p ?o .
    ?o ?op ?oo .
    ?o bdo:personName ?pname .
    ?pname ?pnamep ?pnameo .
  }
  union {
    ?p rdfs:subclassOf* :workCreator .
    ?R_RES ?p ?o .
    ?o bdo:personEvent ?pevent .
    ?pevent ?peventp ?pevento .
  }
  union {
    ?R_RES :workLangScript ?ols .
    ?ols ?olsp ?olso .
    ?ols :language ?lango .
    ?lango ?langop ?langoo .
  }
  union {
    ?R_RES :workLangScript ?ols .
    ?ols :script ?scripto .
    ?scripto ?scriptop ?scriptoo .
  }
  union {
    ?R_RES :workOtherLangScript ?ools .
    ?ools ?oolsp ?oolso .
    ?ools :language ?olango .
    ?olango ?olangop ?olangoo .
  }
  union {
    ?R_RES :workEvent ?we .
    ?we a :PublishedEvent ;
      :onYear ?publishedYear .
  }
  union {
      ?R_RES :workTitle ?o .
      ?o ?op ?oo .
  }
}
