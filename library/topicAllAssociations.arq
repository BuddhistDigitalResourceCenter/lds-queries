#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All resources associated with the given Topic.
#QueryParams=R_RES
#QueryUrl=/lib/topicAllAssociations?R_RES=T132

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the referenced Person

construct {

	#associated Works
	?w a :Work .
    ?w skos:prefLabel ?l .
  	?w adm:license ?lic.
  	?w adm:status ?st .
  	?w adm:access ?acc .
  	?w bdo:workLangScript ?ls.

    ?w bdo:workType ?wT.
    ?w bdo:workExpressionOf ?wE.
    ?w <http://purl.bdrc.io/ontology/tmp/prefLabelExpressionOf> ?litE.
    ?w bdo:workHasExpression ?wH.
    ?w <http://purl.bdrc.io/ontology/tmp/prefLabelHasExpression> ?litH.


  	#associated Lineages
  	?lin a :Lineage .
    ?lin skos:prefLabel ?ll
}
where {

    #associated Works
	{
  		{ ?w :workGenre ?R_RES . }
      union
      { ?w :workIsAbout ?R_RES . } .
    	?w skos:prefLabel ?l .
  		?w adm:license ?lic.
  		?w adm:status ?st .
  		?w adm:access ?acc .
  		?w bdo:workLangScript ?ls.

      OPTIONAL{?w bdo:workType ?wT. ?w bdo:workType bdr:WorkTypeAbstractWork.}.
      OPTIONAL{
        ?w bdo:workExpressionOf ?wE.
        ?wE skos:prefLabel ?litE.
       }.
      OPTIONAL{
        ?w bdo:workHasExpression ?wH.
        ?wH skos:prefLabel ?litH.
      }
	}

	#associated Lineages
	union
	{
		?lin bdo:lineageObject ?R_RES .
    	?lin skos:prefLabel ?ll
	}
}
