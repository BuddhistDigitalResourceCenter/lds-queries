#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All places whose various literals contain the given L_NAME parameter value.
#QueryParams=L_NAME,LG_NAME
#QueryUrl=/lib/placeGraph?L_NAME="'od zer"&LG_NAME=bo-x-ewts

#param.L_NAME.type=string
#param.L_NAME.langTag=LG_NAME
#param.L_NAME.isLucene=true
#param.L_NAME.example="ye shes"

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

CONSTRUCT
{
    ?res tmp:status ?st .
    ?res tmp:matchScore ?sc .
    ?res tmp:labelMatch ?labelMatch.
    ?res tmp:isMain true .
    ?res ?resp ?reso .
    ?res tmp:placeAffiliation ?trad .
    ?res tmp:provider ?provider .
    ?res tmp:originalRecord ?originalRecord .

    ?locatedin skos:prefLabel ?locatedinL .

}
WHERE 
{
  {
    
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
    
    ?res a bdo:Place .
    ?res ?resp ?reso .
    FILTER (?resp = skos:altLabel || ?resp = skos:prefLabel || ?resp = skos:placeEvent || ?resp = bdo:placeLat || ?resp = bdo:placeLong || ?resp = bdo:placeType || ?resp = bdo:placeLocatedIn || ?resp = owl:sameAs || ?resp = tmp:entityScore)
    ?resAdm adm:adminAbout ?res .
    ?resAdm adm:status ?st .
    ?resAdm adm:metadataLegal/adm:provider ?provider .
    
  } union {
    
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
    
    ?res bdo:placeEvent/bdo:placeEventAffiliation ?trad .

  } union {
    
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
    
    ?res bdo:placeLocatedIn ?locatedin .
    ?locatedinAdm adm:adminAbout ?locatedin .
    ?locatedinAdm adm:status bda:StatusReleased .
    ?locatedin skos:prefLabel ?locatedinL .

  }
}
