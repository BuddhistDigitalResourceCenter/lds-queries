#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All works whose various literals contain the given L_NAME parameter value, along with their LICENSE + ACCESS + LANGSCRIPT + STATUS facets.
#QueryParams=L_NAME,LG_NAME
#QueryUrl=/lib/placeGraph?L_NAME="'od zer"&LG_NAME=bo-x-ewts

#param.L_NAME.type=string
#param.L_NAME.langTag=LG_NAME
#param.L_NAME.isLucene=true
#param.L_NAME.example="ye shes"

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

CONSTRUCT
{
    ?res tmp:status ?st .
    ?res tmp:matchScore ?sc .
    ?res tmp:labelMatch ?labelMatch.
    ?res tmp:isMain true .
    ?res ?resp ?reso .
    ?res tmp:placeAffiliation ?trad .

    ?locatedin skos:prefLabel ?locatedinL .

}
WHERE 
{
  {
    
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
    
    ?res a bdo:Place .
    ?res ?resp ?reso .
    FILTER (?resp = bdo:altLabel || ?resp = skos:prefLabel || ?resp = bdo:placeLat || ?resp = bdo:placeLong || ?resp = bdo:placeType || ?resp = bdo:placeLocatedIn)
    ?resAdm adm:adminAbout ?res .
    ?resAdm adm:status ?st .
    
  } union {
    
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
    
    ?res bdo:placeEvent/bdo:placeEventAffiliation ?trad .

  } union {
    
    (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
    
    ?res bdo:placeLocatedIn ?locatedin .
    ?locatedin skos:prefLabel ?locatedinL .

  }
}
